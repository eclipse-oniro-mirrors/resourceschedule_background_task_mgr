# Copyright (c) 2025-2026 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//build/ohos/taihe_idl/taihe.gni")
import("//foundation/resourceschedule/background_task_mgr/bgtaskmgr.gni")

copy_taihe_idl("copy_backgroundTaskManager") {
  sources = [ "idl/ohos.resourceschedule.backgroundTaskManager.taihe" ]
}

subsystem_name = "resourceschedule"
part_name = "background_task_mgr"
taihe_generated_file_path = "$taihe_file_path/out/$subsystem_name/$part_name"

ohos_taihe("run_taihe") {
  taihe_generated_file_path = "$taihe_generated_file_path"
  deps = [ ":copy_backgroundTaskManager" ]
  outputs = [
    "$taihe_generated_file_path/src/ohos.resourceschedule.backgroundTaskManager.ani.cpp",
    "$taihe_generated_file_path/src/ohos.resourceschedule.backgroundTaskManager.abi.c",
  ]
}

taihe_shared_library("background_task_manager_ani") {
  branch_protector_ret = "pac_ret"
  shlib_type = "ani"
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  taihe_generated_file_path = "$taihe_generated_file_path"
  subsystem_name = "$subsystem_name"
  part_name = "$part_name"
  sources = get_target_outputs(":run_taihe")
  include_dirs = [
    "${bgtaskmgr_frameworks_path}/include",
    "${bgtaskmgr_frameworks_path}/common/include",
    "include",
  ]
  sources += [
    "${bgtaskmgr_frameworks_path}/common/src/bgtaskmgr_inner_errors.cpp",
    "src/ani_backgroundtask_subscriber.cpp",
    "src/ani_constructor.cpp",
    "src/ani_task.cpp",
    "src/callback_instance.cpp",
    "src/common.cpp",
    "src/ohos.resourceschedule.backgroundTaskManager.impl.cpp",
  ]
  deps = [
    ":run_taihe",
    "${bgtaskmgr_interfaces_path}/innerkits:bgtaskmgr_innerkits",
  ]

  external_deps = [
    "ability_runtime:ability_context_native",
    "ability_runtime:abilitykit_native",
    "ability_runtime:ani_base_context",
    "ability_runtime:ani_wantagent_common",
    "ability_runtime:runtime",
    "ability_runtime:wantagent_innerkits",
    "c_utils:utils",
    "common_event_service:cesfwk_innerkits",
    "hilog:libhilog",
    "ipc:ipc_core",
    "samgr:samgr_proxy",
  ]
}

generate_static_abc("background_task_manager_abc") {
  base_url = "$taihe_generated_file_path"
  files = [ "$taihe_generated_file_path/@ohos.resourceschedule.backgroundTaskManager.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/background_task_manager_abc.abc"
  dependencies = [ ":run_taihe" ]
}

ohos_prebuilt_etc("background_task_manager_etc") {
  source = "$target_out_dir/background_task_manager_abc.abc"
  module_install_dir = "framework"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  deps = [ ":background_task_manager_abc" ]
}

group("background_task_manager_taihe") {
  deps = [
    ":background_task_manager_ani",
    ":background_task_manager_etc",
  ]
}
